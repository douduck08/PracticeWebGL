<!DOCTYPE html>
<html>

<head>
    <meta charset=utf-8>
    <title>WebGL #6 - Epic Games, PBR Shading</title>
    <style>
        body {
            margin: 0;
            background-color: black;
        }

        canvas {
            width: 100%;
            height: 100%;
        }
    </style>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/90/three.js"></script>
</head>

<body>
    <script type="x-shader/x-vertex" id="vertexShader">
        varying vec3 vNormal;
        varying vec3 vFragPos;
        void main() {
            vec4 worldPos = modelViewMatrix * vec4(position, 1.0);
            gl_Position = projectionMatrix * worldPos;
            vNormal = normalMatrix * normal;
            vFragPos = worldPos.xyz;
        }
    </script>

    <script type="x-shader/x-fragment" id="fragmentShader">
        struct Material {
            vec3 color;         // base color
            float roughness;    // 0~1
            float metallic;     // 0~1
            float specular;     // 0~1
        };
        
        struct PointLight {
            vec3 color;
            vec3 position;
        };

        uniform Material material;
        uniform samplerCube envMap; 
        uniform sampler2D BRDFMap;
        uniform vec3 ambient;
        uniform PointLight light;
        
        varying vec3 vNormal;
        varying vec3 vFragPos;

        float SchlickFresnel (float c) {
            float x = clamp(1.0 - c, 0.0, 1.0);
            float x2 = x * x;
            return x2 * x2 * x;
        }

        float BurleyDiffuse (float NdotL, float NdotV, float LdotH, float roughness) {
            float fresnelLight = SchlickFresnel(NdotL); 
            float fresnelView = SchlickFresnel(NdotV);
            float d90 = 0.5 + 2.0 * LdotH * LdotH * roughness;
            float fact = (1.0 + (d90 - 1.0) * fresnelLight) * (1.0 + (d90 - 1.0) * fresnelView);
            return fact / 3.1415926535;
        }

        float GGXNormalDistribution(float roughness, float NdotH) {
            float alpha = roughness * roughness;
            float NdotHSqr = NdotH * NdotH;
            float fact = alpha / (NdotHSqr * (alpha * alpha - 1.0) + 1.0);
            return fact * fact / 3.1415926535;
        }

        float SchlickGGXGSF (float NdotL, float NdotV, float roughness) {
            roughness = roughness + 1.0;
            float k = roughness * roughness / 8.0;
            float SmithL = NdotL / (NdotL * (1.0 - k) + k);
            float SmithV = NdotV / (NdotV * (1.0 - k) + k);
            return SmithL * SmithV;
        }

        float SphericalGaussianFresnel (float ior, float LdotH) {
            float a = ior - 1.0;
            float b = ior + 1.0;
            float f0 = (a * a) / (b * b);
            return f0 + (1.0 - f0) * SchlickFresnel(LdotH);
        }

        vec3 SchlickFresnelFunction (vec3 specularColor, float LdotH) {
            return specularColor + (vec3(1,1,1) - specularColor) * SchlickFresnel(LdotH);
        }

        void main() {
            vec3 normalDir = normalize(vNormal);
            vec3 lightDir = normalize(light.position - vFragPos);
            vec3 lightReflectDir = reflect(-lightDir, normalDir);
            vec3 viewDir = normalize(cameraPosition - vFragPos);
            vec3 viewReflectDir = normalize(reflect(-viewDir, normalDir));
            vec3 halfDir = normalize(lightDir + viewDir);

            float NdotL = max(0.0, dot(normalDir, lightDir));
            float NdotH = max(0.0, dot(normalDir, halfDir));
            float NdotV = max(0.0, dot(normalDir, viewDir));
            float VdotH = max(0.0, dot(viewDir, halfDir));
            float LdotH = max(0.0, dot(lightDir, halfDir));
            float LdotV = max(0.0, dot(lightDir, viewDir)); 
            float RdotV = max(0.0, dot(lightReflectDir, viewDir));

            vec3 diffuse = material.color * (1.0 - material.metallic);
            diffuse *= BurleyDiffuse (NdotL, NdotV, LdotH, material.roughness);

            vec3 dielectricColor = vec3(0.08 * material.specular);
            vec3 specularColor = mix(dielectricColor, material.color, material.metallic);
            float D= GGXNormalDistribution (material.roughness, NdotH);
            float G = SchlickGGXGSF (NdotL, NdotV, material.roughness);
            vec3 F = SchlickFresnelFunction (specularColor, LdotH);
            vec3 specular = F * ((D * G) / (4.0 * NdotL * NdotV));
            
            vec3 envColor = textureCube(envMap, viewReflectDir).rgb;
            vec2 envBRDF = texture2D(BRDFMap, vec2(material.roughness, NdotV)).xy;
            vec3 indirectDiffuse = ambient * diffuse;  // simple way
            vec3 indirectSpecular = envColor * (specularColor * envBRDF.x + envBRDF.y);

            vec3 attenColor = light.color;
            vec3 lightingModel = indirectDiffuse + indirectSpecular + (diffuse + specular) * attenColor;
            gl_FragColor = vec4(lightingModel, 1);
        }
    </script>

    <script>
        var renderer, clock;
        var camera, scene;
        var meshes = [], uniforms = [];

        init();
        renderUpdate();

        function init() {
            // clock
            clock = new THREE.Clock();

            // camera
            var width = window.innerWidth, height = window.innerHeight;
            camera = new THREE.PerspectiveCamera(70, width / height, 0.01, 100);
            camera.position.set(0, 0, 18);
            // camera = new THREE.OrthographicCamera(width / - 100, width / 100, height / 100, height / - 100, 0.1, 10);
            // camera.position.set(0, 0, 10);

            // scene & buffer
            scene = new THREE.Scene();
            scene.background = new THREE.CubeTextureLoader()
                .setPath('https://raw.githubusercontent.com/mrdoob/three.js/master/examples/textures/cube/pisa/')
                .load(['px.png', 'nx.png', 'py.png', 'ny.png', 'pz.png', 'nz.png']);

            // shader material & mesh
            var vertexShader = document.getElementById('vertexShader').textContent;
            var fragmentShader = document.getElementById('fragmentShader').textContent;
            var ambient = new THREE.Color(1.0, 1.0, 1.0);
            var geometry = new THREE.SphereGeometry(1, 64, 32);
            var BRDFIntegrationMap = new THREE.TextureLoader().load("https://jmonkeyengine.github.io/wiki/jme3/advanced/integrateBrdf.png");

            for (var x = 0; x < 8; x++) {
                for (var y = 0; y < 8; y++) {
                    var uni = {
                        material: {
                            value: {
                                color: new THREE.Color(1, 0.2, 0.2),
                                roughness: 0.125 * x,
                                metallic: 0.125 * y,
                                specular: 0.5,
                            }
                        },
                        envMap: new THREE.Uniform (scene.background),
                        BRDFMap: new THREE.Uniform (BRDFIntegrationMap),
                        ambient: new THREE.Uniform (ambient),
                        light: {
                            value: {
                                color: new THREE.Color(1, 1, 1),
                                position: new THREE.Vector3(-5, 5, 0)
                            }
                        }
                    };

                    var mat = new THREE.ShaderMaterial({
                        uniforms: uni,
                        vertexShader: vertexShader,
                        fragmentShader: fragmentShader,
                        side: THREE.FrontSide
                    });

                    var mesh = new THREE.Mesh(geometry, mat);
                    mesh.position.set(-7 + 2 * x, -7 + 2 * y, 0);

                    uniforms.push(uni);
                    meshes.push(mesh);
                    scene.add(mesh);
                }   
            }

            // renderer
            renderer = new THREE.WebGLRenderer({ antialias: true });
            renderer.setSize(window.innerWidth, window.innerHeight);
            document.body.appendChild(renderer.domElement);
        }

        var test = 0.0;
        function renderUpdate() {
            requestAnimationFrame(renderUpdate);

            var deltaTime = clock.getDelta();

            renderer.render(scene, camera);
        }
    </script>
</body>

</html>